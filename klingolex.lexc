! Hafner/Marques in May/June 2011

! We use multichar symbols for the analysis level of
! our two-level morphology. Temporarly there's also some
! use of them at both levels, e. g. in case of [rover],
! which gets replaced with Foma/XFST processing after
! its generation through this lexc code.
! Also temporarily mutlichar symbols are in use, because
! of our use of flag-diacritics -- important for the use
! of the corresponding suffixes depending on the noun
! type (ability to speak, no ability to speak, body part)
! taken.
Multichar_Symbols
! For noun stuff
[NOUN]
[BIG]
[SMALL]
[PL]
[SG]
[SoCalled]
[apparent]
[definite]
[1PERS]
[2PERS]
[3PERS]
[close] ! how close the speaker is to the object
[far]
[locative]
[from]
[DueTo]
[intended]
@U.type.being@
@U.type.bodypart@
@U.type.else@
@D.type.being@
@C.type@

! For verbal stuff (prefixes)
[VERB]

! For I, you, he/she/it/, we, you, they we use the markers from the noun stuff,
! e. g. [1PERS][PL] for we when we the subject.
! What follows here are markers for the object.
[NONE]
[ME]
[YOUSG] ! you as object in singular
[IT] !(or HIM, OR HER)
[US]
[YOUPL] ! you as object in plural
[THEM]

! For verbal stuff (suffixes)
[oneself]
[oneanother]
[need]
[willing]
[prepared]
[setup]
[afraid]
[statechange]
[resume]
[cause]
[indefiniteSubject]
[ability]
[clearly]
[certainly]
[apparently]
[perfective]
[done]
[continuous]
[progressing]
[when]
[if]
[before]
[while]
[for]
[which]
[interrogative]
[oneWhoDoes]
[rover]

LEXICON Root
  NounStems;
  Verbprefix; 
  
LEXICON Verbprefix
! Verb prefix table, to allow for subject-object relation
!         OBJECT                    
!        None   me   you   Him/her/it   us   you   them
!    I   jI-   -   qa-   vI-     -   Sa-   vI-
!    You   BI-   cho-   -   Da-     ju-   -   Da-
! SUBJ  It/(S)He 0   mu-   Du-   0     nu-   lI-   0
!    We   Maâ€™-   -   pI-   wI-     -   re-   DI-
!   You   Su-   tu-   -   bo-     che-   -   Bo-
!    They   0   mu-   nI-   lu-     nu-   lI-   0
! Source: http://www.judion.de/klingon/ (with a clean table)
  !---------------------------------------------------------------
  ! First person (sg) is subject:
  [SG][1PERS][NONE]:jI Verb; 
  ! [SG][1PERS][ME] w/o prefix; implemented via suffix
  [SG][1PERS][YOUSG]:qa Verb;
  [SG][1PERS][IT]:vI Verb;
  ! [SG][1PERS][US] w/o prefix; implemented via suffix
  [SG][1PERS][YOUPL]:Sa Verb;
  [SG][1PERS][THEM]:vI Verb;
  !---------------------------------------------------------------
  ! Second person (sg) is subject:
  [SG][2PERS][NONE]:BI Verb;
  [SG][2PERS][ME]:cho Verb;
  ! [SG][2PERS][YOUSG] w/o prefix; implemented via suffix
  [SG][2PERS][IT]:Da Verb;
  [SG][2PERS][US]:ju Verb;
  ! [SG][2PERS][YOUPL] w/o prefix; implemented via suffix
  [SG][2PERS][THEM]:Da Verb;
  !---------------------------------------------------------------
  ! Third person (sg) is subject:
  [SG][3PERS][NONE]:0 Verb;
  [SG][3PERS][ME]:mu Verb;
  [SG][3PERS][YOUSG]:Du Verb;
  [SG][3PERS][IT]:0 Verb;
  [SG][3PERS][US]:nu Verb;
  [SG][3PERS][YOUPL]:lI Verb;
  [SG][3PERS][THEM]:0 Verb;
  !---------------------------------------------------------------
  ! First person (pl) is subject:
  [PL][1PERS][NONE]:Ma' Verb;
  ! [PL][1PERS][ME] w/o prefix, implemented via suffix
  [PL][1PERS][YOUSG]:pI Verb;
  [PL][1PERS][IT]:wI Verb;
  ! [PL][1PERS][US] w/o prefix; implemented via suffix
  [PL][1PERS][YOUPL]:re Verb;
  [PL][1PERS][THEM]:DI Verb;
  !---------------------------------------------------------------
  ! Second person (pl) is subject:
  [PL][2PERS][NONE]:Su Verb;
  [PL][2PERS][ME]:tu Verb;
  ! [PL][2PERS][YOUSG] w/o prefix; implemented via suffix
  [PL][2PERS][IT]:bo Verb;
  [PL][2PERS][US]:che Verb;
  ! [PL][2PERS][YOUPL] w/o prefix; implemented via suffix
  [PL][2PERS][THEM]:Bo Verb;
  !---------------------------------------------------------------
  ! Third person (pl) is subject:
  [PL][3PERS][NONE]:0 Verb;
  [PL][3PERS][ME]:mu Verb;
  [PL][3PERS][YOUSG]:nI Verb;
  [PL][3PERS][IT]:lu Verb;
  [PL][3PERS][US]:nu Verb;
  [PL][3PERS][YOUPL]:lI Verb;
  [PL][3PERS][THEM]:0 Verb;
  !---------------------------------------------------------------
  ! For the verbs w/o prefixes  
  Verb;
  !---------------------------------------------------------------

LEXICON Verb
  [VERB]:0 VerbStems;

! Verb stems (=infintive forms) implemented; more can be added here
LEXICON VerbStems
  vut Vtype1; ! to cook
  Qoy Vtype1; ! to hear
  yaj Vtype1; ! to understand
  legh Vtype1; ! to see
  ja' Vtype1; to tell

! Noun stems implemented; more can be added here
! Flag diacritics as mentioned in the README are
! used here for the first time
LEXICON NounStems
  SuS@U.type.else@ Noun; ! wind
  qoH@U.type.else@ Noun; ! fool
  Qagh@U.type.being@ Noun; ! error, mistake
  woQ@U.type.else@ Noun; ! authority, power

! Noun compounds are created here; suffixes apply to last
! stem used
LEXICON Noun
  [NOUN]:0 Ntype1;
  @C.type@ NounStems; ! type of last one determines suffixes

! Noun suffixes type 1: big/small
LEXICON Ntype1
  [BIG]:'a' Ntype2;
  [SMALL]:Hom Ntype2;
  Ntype2;
  
! Noun suffixes type 2: singular/plural
LEXICON Ntype2
  [PL]@U.type.being@:pu'@U.type.being@ Ntype3;
  [PL]@U.type.bodypart@:Du'@U.type.bodypart@ Ntype3;
  [PL]@U.type.else@:mey@U.type.else@ Ntype3;
  [SG]:0 Ntype3;

! Noun suffixes type 3: so called/apparent/definitive
LEXICON Ntype3
  [SoCalled]:qoq Ntype4;
  [apparent]:Hey Ntype4;
  [definite]:na' Ntype4;
  Ntype4;

! Noun suffixes type 4: numerus/close/far
LEXICON Ntype4
  [1PERS]@D.type.being@:wIj@D.type.being@ Ntype5;
  [2PERS]@D.type.being@:lIj@D.type.being@ Ntype5;
  [3PERS]:Daj Ntype5;
  [1PERS]@U.type.being@:wl'@U.type.being@ Ntype5;
  [2PERS]@U.type.being@:lI'@U.type.being@ Ntype5;
  [close]:vam Ntype5;
  [far]:vetlh Ntype5;
  Ntype5;

! Noun suffixes type 5: locative/from/due to/intended
LEXICON Ntype5
  [locative]:Daq #;
  [from]:vo' #;
  [DueTo]:mo' #;
  [intended]:vaD #;
  #;

! Verb suffixes type 1: oneself/one another
LEXICON Vtype1
  [oneself]:'egh Vtype2;
  [oneanother]:chuq Vtype2;
  Vtype2x;

! Intermediate/transitional class for rovers
LEXICON Vtype2
  [rover] Vtype2x;
  Vtype2x;

! Verb suffixes type 2: volition/predisposition
LEXICON Vtype2x
  [need]:nlS Vtype3;
  [willing]:qang Vtype3;
  [prepared]:rup Vtype3; ! in the sense of readiness
  [setup]:beH Vtype3;    ! in the sense of readiness
  [afraid]:vlp Vtype3;
  Vtype3x;

! Intermediate/transitional class for rovers
LEXICON Vtype3
  [rover] Vtype3x;
  Vtype3x;

! Verb suffixes type 3: state change/resume
LEXICON Vtype3x
  [statechange]:choH Vtype4;
  [resume]:qa' Vtype4;
  Vtype4x;

! Intermediate/transitional class for rovers
LEXICON Vtype4
  [rover] Vtype4x;
  Vtype4x;

! Verb suffixes type 4: cause
LEXICON Vtype4x
  [cause]:moH Vtype5;
  Vtype5x;

! Intermediate/transitional class for rovers
LEXICON Vtype5
  [rover] Vtype5x;
  Vtype5x;

! Verb suffixes type 5: indefinite subject/ability (independend of each other)
LEXICON Vtype5x
  [indefiniteSubject]:lu' Vtype6;
  [ability]:laH Vtype6; ! in the sense of could / being able to
  Vtype6x;

! Intermediate/transitional class for rovers
LEXICON Vtype6
  [rover] Vtype6x;
  Vtype6x;

! Verb suffixes type 6: qualification
LEXICON Vtype6x
  [clearly]:chu' Vtype7; ! also: perfectly
  [certainly]:bej Vtype7; ! also: undoubtedly
  [apparently]:law' Vtype7;
  Vtype7x;

! Intermediate/transitional class for rovers
LEXICON Vtype7
  [rover] Vtype7x;
  Vtype7x;

! Verb suffixes type 7: aspect
LEXICON Vtype7x
  [perfective]:pu' Vtype8;  
  [done]:ta' Vtype8;
  [continuous]:taH Vtype8;  
  [progressing]:lH' Vtype8;
  Vtype8x;

! Intermediate/transitional class for rovers
LEXICON Vtype8
  [rover] Vtype8x;
  Vtype8x;

! Verb suffixes type 8: honorific (in higher respect of)
LEXICON Vtype8x
  [honorific]:neS Vtype9;
  Vtype9x;

! Intermediate/transitional class for rovers
LEXICON Vtype9
  [rover] Vtype9x;
  Vtype9x;

! Verb suffixes type 9: syntactic features; for sentences
LEXICON Vtype9x
  [when]:Dl' #;
  [if]:chugh #;
  [before]:pa' #;
  [while]:vlS #;
  [for]:meH #;
  [which]:bogh #;
  [interrogative]:'a' #;  
  [oneWhoDoes]:wl' #; ! one who does, or something that does
  #;
